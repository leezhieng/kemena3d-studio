#version 330 core

const int MAX_BONES         = 128;
const int MAX_BONE_INFLUENCE = 4;

uniform mat4 modelMatrix;
uniform mat4 viewMatrix;
uniform mat4 projectionMatrix;
uniform mat4 normalMatrix;
uniform mat4 lightSpaceMatrix;
uniform mat4 finalBonesMatrices[MAX_BONES];

layout(location = 0) in vec3  vertexPosition;
layout(location = 1) in vec3  vertexColor;
layout(location = 2) in vec2  vertexTexCoord;
layout(location = 3) in vec3  vertexNormal;
layout(location = 4) in vec3  vertexTangent;
layout(location = 5) in vec3  vertexBitangent;
layout(location = 6) in ivec4 boneIDs;
layout(location = 7) in vec4  weights;

out vec3 v_worldPos;
out vec3 v_color;
out vec2 v_texCoord;
out vec4 v_lightSpacePos;
out vec3 v_T;
out vec3 v_B;
out vec3 v_N;

void main()
{
    vec4  totalPos       = vec4(vertexPosition, 1.0);
    vec3  totalNormal    = vec3(0.0);
    vec3  totalTangent   = vec3(0.0);
    vec3  totalBitangent = vec3(0.0);
    float totalWeight    = 0.0;

    for (int i = 0; i < MAX_BONE_INFLUENCE; i++)
    {
        int   boneID = boneIDs[i];
        float weight = weights[i];
        if (boneID < 0 || weight <= 0.0) continue;
        if (boneID >= MAX_BONES)
        {
            totalPos       = vec4(vertexPosition, 1.0);
            totalNormal    = vertexNormal;
            totalTangent   = vertexTangent;
            totalBitangent = vertexBitangent;
            break;
        }
        totalPos       += finalBonesMatrices[boneID] * vec4(vertexPosition, 1.0) * weight;
        mat3 nm         = transpose(inverse(mat3(finalBonesMatrices[boneID])));
        totalNormal    += nm * vertexNormal    * weight;
        totalTangent   += nm * vertexTangent   * weight;
        totalBitangent += nm * vertexBitangent * weight;
        totalWeight    += weight;
    }
    if (totalWeight == 0.0)
    {
        totalPos       = vec4(vertexPosition, 1.0);
        totalNormal    = vertexNormal;
        totalTangent   = vertexTangent;
        totalBitangent = vertexBitangent;
    }

    bool anim    = totalWeight > 0.0;
    vec3 useN    = anim ? totalNormal    : vertexNormal;
    vec3 useT    = anim ? totalTangent   : vertexTangent;
    vec3 useB    = anim ? totalBitangent : vertexBitangent;

    mat3 nm3     = mat3(normalMatrix);
    vec3 worldPos = vec3(modelMatrix * totalPos);

    v_worldPos      = worldPos;
    v_color         = vertexColor;
    v_texCoord      = vertexTexCoord;
    v_lightSpacePos = lightSpaceMatrix * vec4(worldPos, 1.0);
    v_T             = nm3 * useT;
    v_B             = nm3 * useB;
    v_N             = nm3 * useN;
    gl_Position     = projectionMatrix * viewMatrix * vec4(worldPos, 1.0);
}

// --- FRAGMENT ---

#version 330 core

struct Material {
    vec2  tiling;
    vec3  ambient;
    vec3  diffuse;
    vec3  specular;
    float shininess;
    float metallic;
    float roughness;
};

struct SunLight {
    float power;
    vec3  direction;
    vec3  diffuse;
    vec3  specular;
};

struct PointLight {
    float power;
    vec3  position;
    float constant;
    float linear;
    float quadratic;
    vec3  diffuse;
    vec3  specular;
};

struct SpotLight {
    float power;
    vec3  position;
    vec3  direction;
    float cutOff;
    float outerCutOff;
    float constant;
    float linear;
    float quadratic;
    vec3  diffuse;
    vec3  specular;
};

uniform Material  material;
uniform vec3      viewPos;

uniform sampler2D albedoMap;
uniform sampler2D normalMap;
uniform sampler2D metallicRoughnessMap;
uniform sampler2D aoMap;
uniform sampler2D emissiveMap;
uniform bool      has_albedoMap;
uniform bool      has_normalMap;
uniform bool      has_metallicRoughnessMap;
uniform bool      has_aoMap;
uniform bool      has_emissiveMap;

uniform vec3        sceneAmbient;
uniform samplerCube skyboxMap;
uniform bool        skyboxAmbientEnabled;
uniform float       skyboxAmbientStrength;

uniform int       sunLightNum;
uniform SunLight  sunLights[32];
uniform int       pointLightNum;
uniform PointLight pointLights[32];
uniform int       spotLightNum;
uniform SpotLight spotLights[32];

in vec3 v_worldPos;
in vec3 v_color;
in vec2 v_texCoord;
in vec4 v_lightSpacePos;
in vec3 v_T;
in vec3 v_B;
in vec3 v_N;

out vec4 fragColor;

const float PI = 3.14159265359;

float distGGX(float NdotH, float roughness)
{
    float a  = roughness * roughness;
    float a2 = a * a;
    float d  = NdotH * NdotH * (a2 - 1.0) + 1.0;
    return a2 / (PI * d * d);
}

float geoSchlick(float ndotv, float roughness)
{
    float r = roughness + 1.0;
    float k = (r * r) / 8.0;
    return ndotv / (ndotv * (1.0 - k) + k);
}

float geoSmith(float NdotV, float NdotL, float roughness)
{
    return geoSchlick(NdotV, roughness) * geoSchlick(NdotL, roughness);
}

vec3 fresnelSchlick(float cosTheta, vec3 F0)
{
    return F0 + (1.0 - F0) * pow(clamp(1.0 - cosTheta, 0.0, 1.0), 5.0);
}

vec3 calcPBR(vec3 albedo, float metallic, float roughness, vec3 F0,
             vec3 n, vec3 v, vec3 l, vec3 radiance)
{
    vec3  h     = normalize(v + l);
    float NdotH = max(dot(n, h),   0.0);
    float NdotV = max(dot(n, v),   0.0);
    float NdotL = max(dot(n, l),   0.0);
    float NDF   = distGGX(NdotH, roughness);
    float G     = geoSmith(NdotV, NdotL, roughness);
    vec3  F     = fresnelSchlick(max(dot(h, v), 0.0), F0);
    vec3  kD    = (1.0 - F) * (1.0 - metallic);
    vec3  spec  = NDF * G * F / (4.0 * NdotV * NdotL + 0.0001);
    return (kD * albedo / PI + spec) * radiance * NdotL;
}

void main()
{
    vec2 uv = v_texCoord * material.tiling;

    vec4  albedoSample = has_albedoMap ? texture(albedoMap, uv) : vec4(1.0);
    vec3  albedo       = material.diffuse * albedoSample.rgb;
    float metallic     = material.metallic;
    float roughness    = material.roughness;

    if (has_metallicRoughnessMap)
    {
        vec2 mr  = texture(metallicRoughnessMap, uv).bg;
        metallic  *= mr.x;
        roughness *= mr.y;
    }
    roughness = clamp(roughness, 0.04, 1.0);

    vec3 Tn = normalize(v_T);
    vec3 Bn = normalize(v_B);
    vec3 Nn = normalize(v_N);
    vec3 norm;
    if (has_normalMap)
    {
        vec3 tn = texture(normalMap, uv).rgb;
        tn.g    = 1.0 - tn.g;
        tn      = normalize(tn * 2.0 - 1.0);
        norm    = normalize(Tn * tn.x + Bn * tn.y + Nn * tn.z);
    }
    else
    {
        norm = Nn;
    }

    vec3 F0 = mix(vec3(0.04), albedo, metallic);
    vec3 v  = normalize(viewPos - v_worldPos);
    vec3 result = vec3(0.0);

    for (int i = 0; i < sunLightNum; i++)
    {
        vec3 l        = normalize(-sunLights[i].direction);
        vec3 radiance = sunLights[i].diffuse * sunLights[i].power;
        result += calcPBR(albedo, metallic, roughness, F0, norm, v, l, radiance);
    }
    for (int i = 0; i < pointLightNum; i++)
    {
        vec3  l    = normalize(pointLights[i].position - v_worldPos);
        float dist = length(pointLights[i].position - v_worldPos);
        float att  = pointLights[i].power /
                     (pointLights[i].constant + pointLights[i].linear * dist
                      + pointLights[i].quadratic * dist * dist);
        result += calcPBR(albedo, metallic, roughness, F0, norm, v, l, pointLights[i].diffuse * att);
    }
    for (int i = 0; i < spotLightNum; i++)
    {
        vec3  l      = normalize(spotLights[i].position - v_worldPos);
        float theta  = dot(l, normalize(-spotLights[i].direction));
        float eps    = spotLights[i].cutOff - spotLights[i].outerCutOff;
        float intens = clamp((theta - spotLights[i].outerCutOff) / eps, 0.0, 1.0);
        float dist   = length(spotLights[i].position - v_worldPos);
        float att    = spotLights[i].power * intens /
                       (spotLights[i].constant + spotLights[i].linear * dist
                        + spotLights[i].quadratic * dist * dist);
        result += calcPBR(albedo, metallic, roughness, F0, norm, v, l, spotLights[i].diffuse * att);
    }

    float ao      = has_aoMap      ? texture(aoMap,      uv).r : 1.0;
    vec4  emissive = has_emissiveMap ? texture(emissiveMap, uv) : vec4(0.0);

    vec3 ambient = sceneAmbient * material.ambient * albedo;
    if (skyboxAmbientEnabled)
        ambient += texture(skyboxMap, norm).rgb * skyboxAmbientStrength * material.ambient * albedo;

    result = ambient * ao + result + emissive.rgb;
    fragColor = vec4(result, albedoSample.a);
}
